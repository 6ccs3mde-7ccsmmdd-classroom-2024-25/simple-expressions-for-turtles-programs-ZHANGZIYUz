grammar uk.ac.kcl.inf.mdd2.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd2/Turtles"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleProgram:
    statements += Statement*
;

Statement:
    MoveStatement |
    TurnStatement |
    LoopStatement |
    VariableDeclaration
;

VariableDeclaration:
    "var" name=ID "=" value=IntExpression
;

LoopStatement:
    count = IntExpression "times" "do"
      statements += Statement+
    "end"
;

MoveStatement:
    command = MoveCommand "(" steps = IntExpression ")"
;

enum MoveCommand:
    forward | backward
;

TurnStatement:
    "turn" command = TurnCommand "by" degrees = REAL "degrees"
;

enum TurnCommand:
    left | right
;

IntExpression:
    AdditionExpression
;

AdditionExpression returns IntExpression:
    MultiplicationExpression (({AdditionExpression.left=current} "+" | {AdditionExpression.left=current} "-") right=MultiplicationExpression)*
;

MultiplicationExpression returns IntExpression:
    PrimaryExpression (({MultiplicationExpression.left=current} "*" | {MultiplicationExpression.left=current} "/") right=PrimaryExpression)*
;

PrimaryExpression returns IntExpression:
    IntLiteral |
    IntVarExpression |
    "(" IntExpression ")"
;

IntLiteral returns IntExpression:
    val=INT
;

IntVarExpression returns IntExpression:
    var=[VariableDeclaration]
;

REAL returns ecore::EFloat hidden():
    INT? "." INT
;
